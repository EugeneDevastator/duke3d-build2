struct vec{ x,y,z; }

static mtx[9];
()
{
   cls(0); clz(1e32);
 
   vec worldup;
   vecset( worldup, 0, -1, 0 );
   
   vec eyeCenter;
   vec target;
   vecset( eyeCenter, 3,3,3 );
     
   t = klock(0);
   r = 2;
   vecset( target, r*cos(t), 2 + sin(t*3), r*sin(t) );
   
   lookAt( eyeCenter, target, worldUp, mtx );
   setcam(0,-3,-6,0,0);
   setcam2( eyeCenter, mtx );
   
   setcol(0xffffff);
   
   for(i=0; i<10; i++)
   {
      for(j=0; j<10; j++)
      {
         u = -1 + (i / 10) * 2;
         v = -1 + (j / 10) * 2;
         drawsph( 3*u, 0, 3*v, .2 );
      }
   }
   
   // Make a kv6 look at something too
   vec cowPos;
   vecset(cowPos, 3.5, 2, 2 );
   lookAt(cowPos, target, worldup, mtx );
   drawspr("cow.kv6", cowpos.x, cowpos.y, cowpos.z,
                      mtx[0], mtx[1], mtx[2],
                      mtx[3], mtx[4], mtx[5],
                      -mtx[6], -mtx[7], -mtx[8] );
   
   setcol(0,0,511);
   drawsph( target.x, target.y, target.z, .1 );
}

setcam2( vec p, m[9] )
{
   setcam(p.x, p.y, p.z,
          m[0], m[1], m[2],
          m[3], m[4], m[5],
          m[6], m[7], m[8]);
}

lookAt(vec eye, vec pos, vec worldup, mtx[9])
{
   vec fwd;
   vec up;
   vec side;
   
   // find forward dir
   vecsub( fwd, pos, eye );
   normalize(fwd);
   
   // find up vector as close to world up as possible   
   dotprod = vecDot(worldUp,fwd);
   vec part2;
   vecscalar( part2, fwd, dotprod);
   
   vecsub(up, worldUp, part2);
   normalize(up);

   vec side;
   vecCross(side,fwd,up);
   normalize(side);
  
   buildMatrix( mtx, side, up, fwd ); 
}

vecScalar( vec out, vec a, s )
{
   out.x = a.x * s;
   out.y = a.y * s;
   out.z = a.z * s;
}

vecAdd( vec out, vec a, vec b)
{
   out.x = a.x + b.x;
   out.y = a.y + b.y;
   out.z = a.z + b.z;   
}
vecSub( vec out, vec a, vec b)
{
   out.x = a.x - b.x;
   out.y = a.y - b.y;
   out.z = a.z - b.z;   
}

vecCross( vec out, vec a, vec b)
{
   out.x = a.y*b.z - a.z*b.y;
   out.y = a.z*b.x - a.x*b.z;
   out.z = a.x*b.y - a.y*b.x;
}

vecset(vec a, x,y,z){
   a.x = x; a.y = y; a.z = z;
}

vecset(vec a, vec other){
   a.x = other.x; a.y = other.y; a.z = other.z;
}

vecDot( vec a, vec b)
{
   return a.x*b.x + a.y*b.y + a.z*b.z;
}

length( vec v )
{
   return sqrt( vecdot(v,v) );
}

normalize( vec v )
{
   len = length(v);
   if ( len )
   {
      v.x /= len; v.y /= len; v.z /= len;
   }
}

buildMatrix( mtx[9], vec side, vec up, vec fwd )
{
   mtx[0] = side.x; mtx[1] = side.y; mtx[2] = side.z;
   mtx[3] = up.x;   mtx[4] = up.y;   mtx[5] = up.z;
   mtx[6] = fwd.x;  mtx[7] = fwd.y;  mtx[8] = fwd.z;
}