// Written 6. oktober 2011 by Torbjoern Haugen
// takes a vector and creates an orthogonal basis
// for drawing a plane using the two basis vectors that are
// orthonormal to the input vector
struct vec{ x,y,z; }

struct snowflake
{
   vec pos;
   ang;
};

enum{MAX_FLAKES = 512}
static snowflake flakes[MAX_FLAKES];

()
{
   if ( numframes == 0 )
   {
      glsettex("crystal.png");   

      for(i=0; i<MAX_FLAKES; i++)
      {
         rx = (-0.5+rnd)*30;
         ry = (-0.5+rnd)*30;
         rz = (-0.5+rnd)*10;
         vecset(flakes[i].pos,rx,ry,rz);
         flakes[i].ang = rnd*2*PI;
      }
   }
   cls(0xc9c9c9); clz(1e32);
   
   t = klock(0);
   a = mousx/xres*2*PI;
   r = -5;
   ca = cos(a);
   sa = sin(a);
   setcam( sa*r, -0.25, ca*r, a, 0 );

   
   glCullface(GL_NONE); // not actual gl
   glEnable(GL_ALPHA_TEST);
   
   
   for(i=0; i<MAX_FLAKES; i++)
   {
      vec pos, normal;
      pos = flakes[i].pos;
      
      // rotate xz
      ang = flakes[i].ang;
      vecset( normal, cos(t+ang), 0, sin(t+ang));
      drawplane(pos, normal);
      
      vec vel;
      vecset(vel, 0, .01, 0 );
      vecadd( flakes[i].pos, flakes[i].pos, vel );
      


      if ( flakes[i].pos.y > 10 ){
         flakes[i].pos.y = -10;
      }
   }
   
   
   
   sleep(16);
      


}

somePerpVector(vec in, vec out){
   vec zdir; vecset( zdir, 0,0,1);
   veccross(out, in, zdir );
   if ( length(out) == 0 )
   {
      vec ydir; vecset( ydir, 0,1,0);
      veccross(out, in, ydir);
   }
   
}



ortho_basis_form_normal( vec inNormal, vec outA, vec outB ){

   #if 1
      // Create two vectors a and c, perpendicular to normal
      vec not_normal;
      vecset(not_normal, inNormal);
   
         
      
      vec perp;
      vecset( perp, inNormal );
      if (not_normal.x == 0 && not_normal.z == 0){
         not_normal.x += 1;
      }else{
         not_normal.y += 1;
      }

   
      veccross(outA,perp,not_normal);
      normalize(outA);
      veccross(outB,perp,outA);
   
   #else
      // this also works, but causes ambiguities
      somePerpVector(inNormal, outA );
      normalize(outA);
      veccross( outB, outA, inNormal );
   #endif
}


drawplane(vec pos, vec normal)
{   
/*
   // Create two vectors a and c, perpendicular to normal
   vec not_normal;
   vecset(not_normal, normal);
   
   vec perp;
   vecset( perp, normal );
   if (not_normal.x == 0 && not_normal.z == 0){
      not_normal.x += 1;
   }else{
      not_normal.y += 1;
   }
   
   vec a, b;
   veccross(a,perp,not_normal);
   normalize(a);
   veccross(b,perp,a);
    
*/
   vec a,b;
   ortho_basis_form_normal( normal, a, b );
   
   
   // scale a and b down to 1/2 as we will use
   // them in +- dir
   vecscalar(a,a,1.0);
   vecscalar(b,b,1.0);
   
   // Create plane vertices from perpendiculars
   vec neg_a, neg_b;
   vecscalar(neg_a, a, -0.5);   
   vecscalar(neg_b, b, -0.5);

   vec ma_mb; // -a - b
   vec ma_pb; // -a + b
   vec pa_pb; //  a + b
   vec pa_mb; //  a - b
   
   vecadd( ma_mb, neg_a, neg_b);
   vecadd( ma_pb, neg_a, b);
   vecadd( pa_pb, a, b);
   vecadd( pa_mb, a, neg_b);

   vec u0, u1, u2, u3;
   vecset(u0, ma_mb);
   vecset(u1, ma_pb);
   vecset(u2, pa_pb);
   vecset(u3, pa_mb);

   // Translate to final position
   vecadd(u0,pos,u0);
   vecadd(u1,pos,u1);
   vecadd(u2,pos,u2);
   vecadd(u3,pos,u3);

   vec end;
   vecadd(end, pos, normal);

//#define SHOW_VECTORS
#ifdef SHOW_VECTORS
      
   setcol(255,0,0);
   drawArrow(pos, end, 0.03);
   
   vecadd(end, pos, a);
   setcol(0,255,0);
   drawArrow( pos, end, 0.03);
   
   vecadd(end, pos, b);
   setcol(0,0,255);
   drawArrow( pos, end, 0.03);
#endif
   
   setcol(255,255,255);
   
   glBegin(GL_QUADS);
      glTexcoord(0,1);
      glVertex( u0 );   
      glTexcoord(0,0);
      glVertex( u1 );
      glTexcoord(1,0);
      glVertex( u2 );
      glTexcoord(1,1);
      glVertex( u3 );
   glEnd();

//#define _DEBUG
#ifdef _DEBUG
   glBegin(GL_LINE_LOOP);
      glTexcoord(0,0);
      glVertex( u0 );   
      glTexcoord(0,1);
      glVertex( u1 );
      glTexcoord(1,1);
      glVertex( u2 );
      glTexcoord(1,0);
      glVertex( u3 );
   glEnd();
#endif  

}

random_unit( vec v )
{
   v.x = nrnd;
   v.y = nrnd;
   v.z = nrnd;
   l = length(v);
   
   v.x /= l;
   v.y /= l;
   v.z /= l;
}

glVertex( vec v ){
   glVertex( v.x, v.y, v.z );
}

drawArrow(vec s, vec e, scale) // finds direction vector e-s.
{
   //moveto(s.x,s.y,s.z);
   //lineto(e.x,e.y,e.z);
   vec dir;
   vecSub(dir, e, s); 
   
   arrowlen = length(dir);
   if(arrowlen < .1) return; // dont draw small arrows.
   
   polelen = arrowlen * 0.75;
   pointerlen = arrowlen - polelen; // not used...
   normalize(dir);
   
   r1=scale;
   drawcone(s.x,s.y,s.z,r1,
            s.x+dir.x*polelen,s.y+dir.y*polelen,s.z+dir.z*polelen,r1,DRAWCONE_NOCAP1 );         
   drawcone(s.x+dir.x*polelen,s.y+dir.y*polelen,s.z+dir.z*polelen, -r1*3, 
            e.x, e.y, e.z, 0.01,DRAWCONE_CENT);
   
}


tform(m[9], vec v)
{
   x = v.x; y = v.y; z = v.z;
   v.x= m[0] * x + m[1] * y + m[2] * z;
   v.y = m[3] * x + m[4] * y + m[5] * z;
   v.z = m[6] * x + m[7] * y + m[8] * z;
}

vecScalar( vec out, vec a, s )
{
   out.x = a.x * s;
   out.y = a.y * s;
   out.z = a.z * s;
}

vecAdd( vec out, vec a, vec b)
{
   out.x = a.x + b.x;
   out.y = a.y + b.y;
   out.z = a.z + b.z;   
}
vecSub( vec out, vec a, vec b)
{
   out.x = a.x - b.x;
   out.y = a.y - b.y;
   out.z = a.z - b.z;   
}

vecCross( vec out, vec a, vec b)
{
   out.x = a.y*b.z - a.z*b.y;
   out.y = a.z*b.x - a.x*b.z;
   out.z = a.x*b.y - a.y*b.x;
}

vecset(vec a, x,y,z){
   a.x = x; a.y = y; a.z = z;
}

vecset(vec a, vec other){
   a.x = other.x; a.y = other.y; a.z = other.z;
}

vecDot( vec a, vec b)
{
   return a.x*b.x + a.y*b.y + a.z*b.z;
}

length( vec v )
{
   return sqrt( vecdot(v,v) );
}

normalize( vec v )
{
   len = length(v);
   if ( len )
   {
      v.x /= len; v.y /= len; v.z /= len;
   }
}
